apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'


def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        applicationId rootProject.ext.android.applicationId
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.versions.versionCode
        versionName rootProject.ext.versions.versionName
        flavorDimensions rootProject.ext.versions.versionName
        multiDexEnabled true
        ndk {
            //选择要添加的对应 cpu 类型的 .so 库。
            abiFilters 'armeabi-v7a', 'arm64-v8a'
            // 还可以添加 'x86', 'x86_64', 'mips', 'mips64', 'arm64-v8a'
        }
        //设置room的Schema的位置
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            // zipAlign可以让安装包中的资源按4字节对齐，这样可以减少应用在运行时的内存消耗。
            zipAlignEnabled true
            minifyEnabled false
        }
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            // zipAlign可以让安装包中的资源按4字节对齐，这样可以减少应用在运行时的内存消耗。
            zipAlignEnabled true
            minifyEnabled false
        }
    }

    //多渠道打包
    productFlavors {
        jrys_gp{}
        vivo {}
        yamaxun {}
        sanxing {}
    }
    //如果需要在不同渠道统一配置，使用productFlavors.all字段
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${variant.productFlavors[0].name}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    dexOptions {
        javaMaxHeapSize "4g"
        additionalParameters += '--multi-dex'
        additionalParameters += '--set-max-idx-number=48000'
        additionalParameters += '--minimal-main-dex'
    }
//    flavorDimensions "version"
//    productFlavors {
//        tv {
//            dimension "version"
//        }
//        mobile {
//            dimension "version"
//
//        }
//    }
}

def releaseTime() {
    return new Date().format("MMddHHmm", TimeZone.getTimeZone("GMT+08:00"))
}

dependencies {
    implementation project(path: ':tvLibrary')
    implementation project(path: ':Aliyunplayer:AlivcPlayerTools')
    implementation 'com.owen:tv-recyclerview:3.0.0'
    implementation 'xyz.doikki.android.dkplayer:dkplayer-java:3.3.5'
    implementation 'xyz.doikki.android.dkplayer:player-exo:3.3.5'
    implementation 'xyz.doikki.android.dkplayer:player-ijk:3.3.5'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    annotationProcessor rootProject.ext.dependencies["room-compiler"]
    implementation  'com.umeng.umsdk:common:9.4.4'// 必选
    implementation  'com.umeng.umsdk:asms:1.4.1'// 必选
    implementation 'com.google.android.gms:play-services-ads:19.6.0'

    // For Java
    implementation 'androidx.work:work-runtime:2.7.1'

    // For Kotlin
    implementation 'androidx.work:work-runtime-ktx:2.7.1'
}